#pragma kernel Main

#define TWO_PI      6.28318530717958647693
#define MAX_ZONES 9

struct SourceVertex
{
    float3 positionWS;
    float3 normalOS;
    float2 uv;
    float3 color;
};

struct DrawVertex
{
    float3 positionWS;
    float2 uv;
};

struct DrawTriangle
{
    float3 normalOS;
    float3 diffuseColor;
    float4 extraBuffer;
    DrawVertex vertices[3];
};

struct IndirectArgs
{
    uint numVerticesPerInstance;
    uint numInstances;
    uint startVertexIndex;
    uint startInstanceIndex;
};

StructuredBuffer<SourceVertex> _SourceVertices;
StructuredBuffer<int> _VisibleIDBuffer;
StructuredBuffer<float> _CutBuffer;
AppendStructuredBuffer<DrawTriangle> _DrawTriangles;
RWStructuredBuffer<IndirectArgs> _IndirectArgsBuffer;

#define GRASS_BLADES 10
#define GRASS_SEGMENTS 3
#define GRASS_NUM_VERTICES_PER_BLADE (GRASS_SEGMENTS * 2)+ 1

int _NumSourceVertices;
float _Time;
float _GrassRandomHeightMin, _GrassRandomHeightMax;
float _WindSpeed;
float _WindStrength;
float _BladeRadius;
float _BladeForward;
float _BladeCurve;
float _BottomWidth;
float _MaxBladesPerVertex;
float _MaxSegmentsPerBlade;
float _MinHeight, _MaxHeight;
float _MinWidth, _MaxWidth;
float _MinFadeDist, _MaxFadeDist;
float _InteractorStrength;
uniform float4 _InteractorData[100];
uniform float _InteractorsLength;
uniform float3 _CameraPositionWS;
float4 _ZonePositions[MAX_ZONES];
float4 _ZoneScales[MAX_ZONES];
float4 _ZoneColors[MAX_ZONES];
float4 _ZoneWidthHeights[MAX_ZONES];
int _ZoneCount;

float grassWidth, grassHeight;

bool IsInAnyZone(float3 position, out int zoneIndex)
{
    zoneIndex = -1;
    for (int i = _ZoneCount - 1; i >= 0; i--)
    {
        if (_ZonePositions[i].w < 0.5) continue;

        float3 delta = abs(position - _ZonePositions[i].xyz);
        if (all(delta <= _ZoneScales[i].xyz * 0.5))
        {
            zoneIndex = i;
            return true;
        }
    }
    return false;
}

float random(float3 co)
{
    return frac(sin(dot(co.xyz, float3(12.9898, 78.233, 53.539))) * 43758.5453);
}

float RandomRange(float3 seed, float min, float max)
{
    float rand = random(seed);
    return lerp(min, max, rand);
}

uint FastHash(uint x)
{
    x ^= x >> 16;
    x *= 0x7feb352dU;
    x ^= x >> 15;
    x *= 0x846ca68bU;
    x ^= x >> 16;
    return x;
}

uint Hash3Pos(float3 pos, uint index)
{
    uint x = asuint(pos.x * 100);
    uint y = asuint(pos.y * 100);
    uint z = asuint(pos.z * 100);
    uint hash = x ^ (y << 8) ^ (z << 16) ^ index;
    return FastHash(hash);
}

float HashToFloat01(uint hash)
{
    return float(hash & 0xFFFFFF) / 0x1000000;
}

float3x3 AngleAxis3x3(float angle, float3 axis)
{
    float c, s;
    sincos(angle, s, c);

    float t = 1 - c;
    float x = axis.x;
    float y = axis.y;
    float z = axis.z;

    return float3x3(
        t * x * x + c, t * x * y - s * z, t * x * z + s * y,
        t * x * y + s * z, t * y * y + c, t * y * z - s * x,
        t * x * z - s * y, t * y * z + s * x, t * z * z + c);
}

float3x3 CreateTangentToWorld(float3 normal)
{
    float3 tangent;
    if (abs(normal.x) > abs(normal.z))
        tangent = float3(-normal.y, normal.x, 0);
    else
        tangent = float3(0, -normal.z, normal.y);
    tangent = normalize(tangent);

    float3 bitangent = normalize(cross(normal, tangent));

    return float3x3(
        tangent.x, normal.x, bitangent.x,
        tangent.y, normal.y, bitangent.y,
        tangent.z, normal.z, bitangent.z
    );
}

float2 GetSimpleBladePosition(float index, float totalBlades, float2 randValues)
{
    float gridSize = sqrt(totalBlades);
    float2 gridPos;
    gridPos.x = fmod(index, gridSize) / gridSize;
    gridPos.y = floor(index / gridSize) / gridSize;
    gridPos = gridPos - 0.5;
    float2 randomOffset = (randValues - 0.5) * 0.8;
    float distanceFromCenter = length(gridPos);
    float maxOffset = 0.8 * (1.0 - distanceFromCenter);
    randomOffset *= maxOffset;
    return gridPos + randomOffset;
}

DrawVertex GrassVertex(float3 positionWS, float width, float height,
                       float offset, float2 uv, float curve, float3 normal, float3x3 rotationMatrix)
{
    DrawVertex output;
    float3x3 tangentToWorld = CreateTangentToWorld(normal);
    float3 localOffset = float3(width, height, curve + offset);
    float3 worldOffset = mul(tangentToWorld, localOffset);
    worldOffset = mul(rotationMatrix, worldOffset);
    output.positionWS = positionWS + worldOffset;
    output.uv = uv;
    return output;
}

half3 CalculationWind(half3 positionWS, half grassHeight)
{
    half timeWind = _Time.x * _WindSpeed;
    half posX = positionWS.x;
    half posZ = positionWS.z;
    half3 wind = half3(
        sin(timeWind + posX) + sin(timeWind + posZ * 2) + sin(timeWind * 0.1 + posX),
        0,
        cos(timeWind + posX * 2) + cos(timeWind + posZ * 2));
    wind *= grassHeight * _WindStrength;
    return wind;
}

float3 CalculateInteraction(float3 grassPosition)
{
    float3 totalBend = float3(0, 0, 0);

    for (int i = 0; i < _InteractorsLength; i++)
    {
        float3 interactorPos = _InteractorData[i].xyz;
        float interactorRadius = _InteractorData[i].w;
        float3 distanceVector = grassPosition - interactorPos;
        float distance = length(distanceVector);

        if (distance < interactorRadius)
        {
            float strength = (1.0 - distance / interactorRadius) * _InteractorStrength;
            float3 bendDirection = normalize(distanceVector);
            bendDirection.y = 0;
            totalBend += bendDirection * strength;
        }
    }
    return totalBend;
}

[numthreads(256, 1, 1)]
void Main(uint id : SV_DispatchThreadID)
{
    if ((int)id >= _NumSourceVertices) return;

    int visibleID = _VisibleIDBuffer[id];
    if (visibleID == -1) return;

    SourceVertex sv = _SourceVertices[visibleID];

    half distanceFromCamera = distance(sv.positionWS, _CameraPositionWS);
    half distanceFade = 1 - saturate((distanceFromCamera - _MinFadeDist) / (_MaxFadeDist - _MinFadeDist));

    if (distanceFade < 0) return;

    // LOD에 따른 세그먼트 수 조절
    half numSegmentsPerBlade = min(GRASS_SEGMENTS, _MaxSegmentsPerBlade);
    half numTrianglesPerBlade = (numSegmentsPerBlade - 1) * 2 + 1;

    // 거리에 따른 blade 수 조절
    half lodBladeCount = lerp(_MaxBladesPerVertex, 1, saturate(distanceFromCamera / (_MaxFadeDist * 0.7)));
    half numBladesPerVertex = ceil(lodBladeCount * distanceFade);

    if (numBladesPerVertex <= 0) return;
    
    half3 grassNormal = normalize(sv.normalOS);

    grassWidth = sv.uv.x;
    grassHeight = sv.uv.y;

    uint localVertexCount = 0;
    half cut = _CutBuffer[visibleID];

    uint heightHash = Hash3Pos(sv.positionWS, 0);
    half randomOffset = lerp(_GrassRandomHeightMin, _GrassRandomHeightMax, HashToFloat01(heightHash));
    grassHeight = clamp(grassHeight + randomOffset, _MinHeight, _MaxHeight);
    grassWidth = clamp(grassWidth, _MinWidth, _MaxWidth);

    int zoneIndex;
    if (IsInAnyZone(sv.positionWS, zoneIndex))
    {
        grassHeight *= _ZoneWidthHeights[zoneIndex].y;
        grassWidth *= _ZoneWidthHeights[zoneIndex].x;
    }

    // 거리에 따른 크기 감소
    half sizeFade = saturate(1 - (distanceFromCamera - _MinFadeDist) / (_MaxFadeDist - _MinFadeDist));
    grassWidth *= sizeFade * sizeFade;
    grassHeight *= sizeFade;

    _BladeForward *= grassHeight;

    half3 wind = CalculationWind(sv.positionWS, grassHeight);
    half3 interaction = CalculateInteraction(sv.positionWS);

    for (int j = 0; j < numBladesPerVertex; j++)
    {
        DrawVertex drawVertices[GRASS_NUM_VERTICES_PER_BLADE];

        uint baseHash = Hash3Pos(sv.positionWS, j);
        float randVar1 = HashToFloat01(FastHash(baseHash));

        float2 bladePos = GetSimpleBladePosition(j, numBladesPerVertex, float2(randVar1, randVar1));
        float offset = _BladeRadius * length(bladePos) * (0.5 + randVar1 * 0.5);
        half3x3 facingRotationMatrix = AngleAxis3x3(randVar1 * TWO_PI, grassNormal);

        for (int i = 0; i < numSegmentsPerBlade; i++)
        {
            half t = i / numSegmentsPerBlade;
            half segmentHeight = grassHeight * t;
            half segmentWidth = grassWidth * (1 - t);

            segmentWidth = i == 0 ? _BottomWidth * segmentWidth : segmentWidth;

            half3 newPos = i == 0
                               ? sv.positionWS
                               : sv.positionWS + (wind + grassNormal * grassHeight + interaction) * t;
            half curveStrength = pow(abs(t), _BladeCurve) * _BladeForward;

            drawVertices[i * 2] = GrassVertex(newPos, segmentWidth, segmentHeight,
                                              offset, half2(0, t), curveStrength, grassNormal, facingRotationMatrix);
            drawVertices[i * 2 + 1] = GrassVertex(newPos, -segmentWidth, segmentHeight,
                                                  offset, half2(1, t), curveStrength, grassNormal,
                                                  facingRotationMatrix);
        }

        half3 topPosOS = sv.positionWS + wind + interaction + grassNormal * grassHeight;
        drawVertices[numSegmentsPerBlade * 2] = GrassVertex(topPosOS, 0, grassHeight, offset,
                                                            half2(0.5, 1), _BladeForward, grassNormal,
                                                            facingRotationMatrix);

        for (int k = 0; k < numTrianglesPerBlade; ++k)
        {
            DrawTriangle tri = (DrawTriangle)0;
            tri.normalOS = grassNormal;
            tri.diffuseColor = sv.color;
            tri.extraBuffer = half4(cut, distanceFade, 0, 0); // distanceFade 값을 전달

            tri.vertices[0] = drawVertices[k];
            tri.vertices[1] = drawVertices[k + 1];
            tri.vertices[2] = drawVertices[k + 2];
            _DrawTriangles.Append(tri);

            localVertexCount += 3;
        }
    }

    InterlockedAdd(_IndirectArgsBuffer[0].numVerticesPerInstance, localVertexCount);
}
